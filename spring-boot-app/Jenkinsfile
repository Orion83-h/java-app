pipeline {
    
    agent {
        label 'docker-host'
    }
            
    tools {
        maven 'maven3'
    }

    parameters {
        string(name: 'BUILD_NUM_TO_KEEP', defaultValue: '-1', description: 'Keep all builds based on the number')
        string(name: 'BUILD_DAYS_TO_KEEP', defaultValue: '7', description: 'Discard builds older than 7 days')
        string(name: 'BUILD_ARTIFACT_NUM_TO_KEEP', defaultValue: '-1', description: 'Keep artifacts for all builds based on the number')
        string(name: 'BUILD_ARTIFACT_DAYS_TO_KEEP', defaultValue: '2', description: 'Discard artifacts older than 2 days')
        string(name: 'CONTAINER_PORT', defaultValue: '8080', description: 'Port to expose on the container')
        string(name: 'BRANCH_NAME', defaultValue: 'main', description: 'Branch to build')
        choice(name: 'ENVIRONMENT', choices: ['dev', 'test', 'prod'], description: 'Deployment environment')
        choice(name: 'TRIVY_SEVERITY', choices: ['LOW,MEDIUM', 'HIGH,CRITICAL', 'CRITICAL'], description: 'Severity levels for Trivy to check')
    }

    options {
        buildDiscarder(logRotator(
            numToKeepStr: params.BUILD_NUM_TO_KEEP,
            daysToKeepStr: params.BUILD_DAYS_TO_KEEP,
            artifactNumToKeepStr: params.BUILD_ARTIFACT_NUM_TO_KEEP,
            artifactDaysToKeepStr: params.BUILD_ARTIFACT_DAYS_TO_KEEP
        ))
        quietPeriod(30)
        timestamps()
    }

    environment {
        GIT_URL = 'https://github.com/Orion83-h/java-app.git'
        GIT_CREDS = 'gitAuth'
        GHCR_USERNAME = 'Orion83-h'
        DOCKERFILE = 'spring-boot-app/Dockerfile.dev'
        DOCKER_BUILDKIT = '1'
        IMAGE_NAME = "colanta06/${env.JOB_NAME}-${env.BUILD_NUMBER}"
        IMAGE_TAG = "${env.BUILD_NUMBER}"
        CONTAINER_NAME = 'java-app'
        SONAR_ORG = 'sam237'
        SONAR_PROJECT_KEY = 'sam237_java-app'
        TRIVY_CACHE_DIR = '/tmp/trivy'
        TRIVY_SCAN_STATUS = '0'
    }

    stages {
        stage('Pull Src Code') {
            steps {
                echo "====++++Pulling Source Code From Repo++++===="
                checkout([$class: 'GitSCM',
                    branches: [[name: "*/${params.BRANCH_NAME}"]],
                    doGenerateSubmoduleConfigurations: false,
                    extensions: [
                        [$class: 'PruneStaleBranch'],
                        [$class: 'CleanBeforeCheckout'],
                        [$class: 'CloneOption', depth: 1, noTags: false, reference: '', shallow: true]
                    ],
                    userRemoteConfigs: [[credentialsId: env.GIT_CREDS, url: env.GIT_URL]]
                ])
            }
        }

        stage('Build and Test') {
            steps {
                echo "====++++Building Maven Project++++===="
                sh 'ls -ltr'
                sh 'cd spring-boot-app && mvn clean package'
                sh 'ls -ltr spring-boot-app/target'
            }
        }

        stage('SonarCloud Analysis') {
            steps {
                echo "====++++Sonar Code Quality++++===="
                withSonarQubeEnv('SonarCloud') {
                    script {
                        def SCANNER_HOME = tool name: 'SonarScanner', type: 'hudson.plugins.sonar.SonarRunnerInstallation'
                        sh """
                            ${SCANNER_HOME}/bin/sonar-scanner \
                            -Dsonar.organization=$SONAR_ORG \
                            -Dsonar.projectKey=$SONAR_PROJECT_KEY \
                            -Dsonar.java.binaries=spring-boot-app/target
                        """
                    }
                }
            }
        }

        stage('Check Dockerfile') {
            steps {
                echo "====++++Checking Dockerfile++++===="
                script {
                    echo "Checking Dockerfile at path: ${env.DOCKERFILE}"
                    if (!fileExists(env.DOCKERFILE)) {
                        error "Dockerfile does not exist: ${env.DOCKERFILE}"
                    }
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                echo "====++++Building Docker Image++++===="
                script {
                    def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"

                    try {
                        sh """
                            docker build \
                                --no-cache \
                                --pull \
                                --build-arg BUILDKIT_INLINE_CACHE=1 \
                                -t ${imageName} \
                                -f ${env.DOCKERFILE} .
                        """
                    } catch (Exception e) {
                        error "Failed to build Docker image: ${e.getMessage()}"
                    }
                }
            }
        }

        stage('Trivy Scan') { 
            steps {
                echo "====++++ Scanning Docker Image with Trivy ++++===="
                script {
                    def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"
                    def reportDir = "trivy-reports"
                    
                    sh "mkdir -p ${reportDir}"
                    sh 'trivy --version'
                    sh 'trivy image --download-db-only'

                    sh """
                        if [ ! -f contrib/html.tpl ]; then
                            mkdir -p contrib
                            wget -O contrib/html.tpl https://raw.githubusercontent.com/aquasecurity/trivy/main/contrib/html.tpl
                        fi
                    """

                    try {
                        withCredentials([
                            usernamePassword(
                                credentialsId: 'gitAuth',
                                usernameVariable: 'GHCR_USERNAME',
                                passwordVariable: 'GHCR_TOKEN'
                            )
                        ]) {
                            env.TRIVY_SCAN_STATUS = sh(
                                script: """
                                    trivy image \
                                        --exit-code 1 \
                                        --cache-dir ${env.TRIVY_CACHE_DIR} \
                                        --severity ${params.TRIVY_SEVERITY} \
                                        --no-progress \
                                        --format template \
                                        --template "@contrib/html.tpl" \
                                        --output ${reportDir}/trivy-report.html \
                                        ${imageName} > ${WORKSPACE}/trivyresults.txt
                                """,
                                returnStatus: true
                            ).toString()
                        }

                        if (fileExists("${reportDir}/trivy-report.html")) {
                            archiveArtifacts artifacts: "${reportDir}/trivy-report.html", fingerprint: true
                        } else {
                            echo "Trivy report not found; skipping artifact archival."
                        }

                        if (env.TRIVY_SCAN_STATUS == '1') {
                            echo "No vulnerabilities found with severity ${params.TRIVY_SEVERITY}"
                        } else if (env.TRIVY_SCAN_STATUS.toInteger() >= 1) {
                            error "Trivy scan failed with exit code ${env.TRIVY_SCAN_STATUS}"
                        }
                    } catch (Exception e) {
                        unstable "Trivy scan encountered an error: ${e.getMessage()}"
                        throw e
                    }
                }
            }
        }

        stage('Push Docker Image') {
            when {
                anyOf {
                    expression { env.TRIVY_SCAN_STATUS == '0' }
                    expression { 
                        env.TRIVY_SCAN_STATUS == '1' && 
                        params.TRIVY_SEVERITY in ['LOW,MEDIUM'] 
                    }
                }
            }
            steps {
                echo "====++++Pushing Docker Image to Registry++++===="
                script {
                    def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"
                    def maxRetries = 3
                    def retryInterval = 10

                    withDockerRegistry(credentialsId: 'dockerId') {
                        retry(maxRetries) {
                            try {
                                sh "docker push ${imageName}"
                            } catch (Exception e) {
                                sleep(time: retryInterval, unit: 'SECONDS')
                                error "Docker push failed: ${e.getMessage()}"
                            }
                        }
                    }
                }
            }
        } 

        stage('Smoke Test') {
            when {
                expression { env.TRIVY_SCAN_STATUS in ['0', '1'] }
            }
            steps {
                echo "====++++Executing Smoke Test++++===="
                script {
                    def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"
                    def hostPort = 8081 // Use a fixed port for the application
        
                    try {
                        // Run the Docker container with the fixed port mapping
                        sh """
                            docker run --name ${env.CONTAINER_NAME} \
                                -d \
                                -p ${hostPort}:${params.CONTAINER_PORT} \
                                ${imageName}
                        """
        
                        // Ensure `check.sh` is executable
                        sh "chmod +x ${WORKSPACE}/spring-boot-app/check.sh"
        
                        // Run health check using the fixed port
                        sh """
                            sleep 30
                            APP_PORT=${hostPort} ${WORKSPACE}/spring-boot-app/check.sh
                        """
                    } catch (Exception e) {
                        error "Smoke test failed: ${e.getMessage()}"
                    } finally {
                        sh """
                            docker stop ${env.CONTAINER_NAME} || true
                            docker rm -f ${env.CONTAINER_NAME} || true
                        """
                    }
                }
            }
        }

        stage('Cleanup') {
            steps {
                echo "====++++Cleaning up++++===="
                script {
                    def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"
                    sh """
                        docker rmi -f ${imageName} || true
                        docker image prune -f
                        docker container prune -f
                        rm -rf ${env.TRIVY_CACHE_DIR} || true
                    """
                }
            }
        }
    }

    post {
        success {
            script {
                def reportUrl = "${env.BUILD_URL}artifact/trivy-reports/trivy-report.html"
                def imageName = "${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}"

                 // Trigger downstream Helm Chart update job
                build job: 'helm-chart-update', 
                    parameters: [
                        string(name: 'IMAGE_NAME', value: "${env.IMAGE_NAME}"),
                        string(name: 'IMAGE_TAG', value: "${env.IMAGE_TAG}"),
                        string(name: 'ENVIRONMENT', value: "${params.ENVIRONMENT}")
                   ]
               // Email notification    
                emailext (
                    to: 'samuelhaddison71@gmail.com',
                    subject: "SUCCESS: ${env.JOB_NAME} - Build ${env.BUILD_NUMBER}",
                    body: """
                        <h2>Build Succeeded</h2>
                        <p>Job: ${env.JOB_NAME}</p>
                        <p>Build Number: ${env.BUILD_NUMBER}</p>
                        <p>Environment: ${params.ENVIRONMENT}</p>
                        <p>Image: ${env.IMAGE_NAME}:${env.IMAGE_TAG}-${params.ENVIRONMENT}</p>
                        <p>Trivy Scan Status: ${env.TRIVY_SCAN_STATUS == '0' ? 'No vulnerabilities' : 
                        env.TRIVY_SCAN_STATUS == '1' ? 'Vulnerabilities found' : 'Scan failed'}</p>
                        <p>Trivy Scan Report: <a href="${reportUrl}">View Report</a></p>
                        <p>Build URL: <a href="${env.BUILD_URL}">View Build Details</a></p>
                    """,
                    mimeType: 'text/html',
                    attachmentsPattern: 'trivy-reports/**'
                )
            }
        }
        failure {
            script {
                def reportUrl = "${env.BUILD_URL}artifact/trivy-reports/trivy-report.html"
                emailext (
                    to: 'samuelhaddison71@gmail.com',
                    subject: "FAILED: ${env.JOB_NAME} - Build #${env.BUILD_NUMBER}",
                    body: """
                        <h2>Build Failed</h2>
                        <p>Job: ${env.JOB_NAME}</p>
                        <p>Build Number: ${env.BUILD_NUMBER}</p>
                        <p>Environment: ${params.ENVIRONMENT}</p>
                        <p>Failed Stage: ${currentBuild.result}</p>
                        <p>Trivy Scan Status: ${env.TRIVY_SCAN_STATUS == '0' ? 'No vulnerabilities' : 
                        env.TRIVY_SCAN_STATUS == '1' ? 'Vulnerabilities found' : 'Scan failed'}</p>
                        <p>Trivy Scan Report: <a href="${reportUrl}">View Report</a></p>
                        <p>Build URL: <a href="${env.BUILD_URL}">View Build Details</a></p>
                    """,
                    mimeType: 'text/html',
                    attachmentsPattern: 'trivy-reports/**'
                )
            }
        }
        always {
            cleanWs(
                cleanWhenNotBuilt: false,
                deleteDirs: true,
                disableDeferredWipeout: true,
                notFailBuild: true
            )
        }
    }
}
